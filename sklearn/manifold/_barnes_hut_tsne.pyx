# cython: boundscheck=False
# cython: wraparound=False
# cython: cdivision=True
# Author: Christopher Moody <chrisemoody@gmail.com>
# Author: Nick Travers <nickt@squareup.com>
# Implementation by Chris Moody & Nick Travers
# See http://homepage.tudelft.nl/19j49/t-SNE.html for reference
# implementations and papers describing the technique


from libc.stdlib cimport malloc, free
from libc.stdio cimport printf
from libc.math cimport sqrt
cimport numpy as np
import numpy as np

cdef extern from "math.h":
    float fabsf(float x) nogil

# Round points differing by less than this amount
# effectively ignoring differences near the 32bit 
# floating point precision
cdef float EPSILON = 1e-6

# This is effectively an ifdef statement in Cython
# It allows us to write printf debugging lines
# and remove them at compile time
cdef enum:
    DEBUGFLAG = 0

cdef extern from "time.h":
    # Declare only what is necessary from `tm` structure.
    ctypedef long clock_t
    clock_t clock() nogil
    double CLOCKS_PER_SEC


cdef struct QuadOctNode:
    # Keep track of the center of mass
    float[3] cum_com
    # If this is a leaf, the position of the particle within this leaf 
    float[3] cur_pos
    # The number of particles including all 
    # nodes below this one
    long cum_size
    # Number of particles at this node
    long size
    # Index of the particle at this node
    long point_index
    # level = 0 is the root node
    # And each subdivision adds 1 to the level
    long level
    # Left edge of this node, normalized to [0,1]
    float[3] left_edge
    # The center of this node, equal to left_edge + w/2.0
    float[3] center     
    # The width of this node -- used to calculate the opening
    # angle.
    float[3] width
    # The half width of this node -- used to speed up certain calculations.
    float[3] half_width

    # Does this node have children?
    # Default to leaf until we add particles
    int is_leaf
    # Keep pointers to the child nodes
    # For a QuadTree, only the first two elements are used,
    # whereas for an OctTree, all three are used.
    QuadOctNode *children[2][2][2]
    # Keep a pointer to the parent
    QuadOctNode *parent
    # Pointer to the tree this node belongs too
    QuadOctTree* tree

cdef struct QuadOctTree:
    # Holds a pointer to the root node
    QuadOctNode* root_node 
    # Number of dimensions in the ouput
    int dimension
    # Total number of cells
    long n_cells
    # Total number of particles
    long n_particles
    # Spit out diagnostic information?
    int verbose

cdef QuadOctTree* init_tree(float[:] left_edge, float[:] width, int dimension, 
                     int verbose) nogil:
    # tree is freed by free_tree
    cdef QuadOctTree* tree = <QuadOctTree*> malloc(sizeof(QuadOctTree))
    tree.dimension = dimension
    tree.n_cells = 0
    tree.n_particles = 0
    tree.verbose = verbose
    tree.root_node = create_root(left_edge, width, dimension)
    tree.root_node.tree = tree
    tree.n_cells += 1
    if DEBUGFLAG:
        printf("[t-SNE] Tree initialised. Left_edge = (%1.9e, %1.9e, %1.9e)\n",
               left_edge[0], left_edge[1], left_edge[2])
        printf("[t-SNE] Tree initialised. Width = (%1.9e, %1.9e, %1.9e)\n",
                width[0], width[1], width[2])
    return tree

cdef QuadOctNode* create_root(float[:] left_edge, float[:] width, int dimension) nogil:
    # Create a default root node
    cdef int ax
    # root is freed by free_tree
    root = <QuadOctNode*> malloc(sizeof(QuadOctNode))
    root.is_leaf = 1
    root.parent = NULL
    root.level = 0
    root.cum_size = 0
    root.size = 0
    root.point_index = -1
    for ax in range(dimension):
        root.width[ax] = width[ax]
        root.half_width[ax] = width[ax] / 2.0
        root.left_edge[ax] = left_edge[ax]
        root.center[ax] = 0.0
        root.cum_com[ax] = 0.
        root.cur_pos[ax] = -1.
    if DEBUGFLAG:
        printf("[t-SNE] Created root node %p\n", root)
    return root

cdef QuadOctNode* create_child(QuadOctNode *parent, int[3] offset) nogil:
    # Create a new child node with default parameters
    cdef int ax
    # these children are freed by free_recursive
    child = <QuadOctNode *> malloc(sizeof(QuadOctNode))
    child.is_leaf = 1
    child.parent = parent
    child.level = parent.level + 1
    child.size = 0
    child.cum_size = 0
    child.point_index = -1
    child.tree = parent.tree
    for ax in range(parent.tree.dimension):
        child.width[ax] = parent.half_width[ax]
        child.half_width[ax] = parent.half_width[ax] / 2.0
        child.left_edge[ax] = parent.left_edge[ax] + offset[ax] * parent.half_width[ax]
        child.center[ax] = child.left_edge[ax] + child.half_width[ax]
        child.cum_com[ax] = 0.
        child.cur_pos[ax] = -1.
    child.tree.n_cells += 1
    return child

cdef QuadOctNode* select_child(QuadOctNode *node, float[3] pos, long index) nogil:
    # Find which sub-node a position should go into
    # And return the appropriate node
    cdef int[3] offset
    cdef int ax
    cdef QuadOctNode* child
    cdef int error
    # In case we don't have 3D data, set it to zero
    for ax in range(3):
        offset[ax] = 0
    for ax in range(node.tree.dimension):
        offset[ax] = (pos[ax] - (node.left_edge[ax] + node.half_width[ax])) > 0.
    child = node.children[offset[0]][offset[1]][offset[2]]
    if DEBUGFLAG:
        printf("[t-SNE] Offset [%i, %i] with LE [%f, %f]\n",
               offset[0], offset[1], child.left_edge[0], child.left_edge[1])
    return child

cdef void subdivide(QuadOctNode* node) nogil:
    # This instantiates 4 or 8 nodes for the current node
    cdef int i = 0
    cdef int j = 0
    cdef int k = 0
    cdef int[3] offset
    node.is_leaf = False
    for ax in range(3):
        offset[ax] = 0
    if node.tree.dimension > 2:
        krange = 2
    else:
        krange = 1
    for i in range(2):
        offset[0] = i
        for j in range(2):
            offset[1] = j
            for k in range(krange):
                offset[2] = k
                node.children[i][j][k] = create_child(node, offset)


cdef int insert(QuadOctNode *root, float pos[3], long point_index, long depth, long
        duplicate_count) nogil:
    # Introduce a new point into the tree
    # by recursively inserting it and subdividng as necessary
    # Carefully treat the case of identical points at the same node
    # by increasing the root.size and tracking duplicate_count
    cdef QuadOctNode *child
    cdef long i
    cdef int ax
    cdef int not_identical = 1
    cdef int dimension = root.tree.dimension
    if DEBUGFLAG:
        printf("[t-SNE] [d=%i] Inserting pos %i [%f, %f] duplicate_count=%i"
                "into child %p\n", depth, point_index, pos[0], pos[1],
                duplicate_count, root)    
    # Increment the total number points including this
    # node and below it
    root.cum_size += duplicate_count
    # Evaluate the new center of mass, weighting the previous
    # center of mass against the new point data
    cdef double frac_seen = <double>(root.cum_size - 1) / (<double>
            root.cum_size)
    cdef double frac_new  = 1.0 / <double> root.cum_size
    # Assert that duplicate_count > 0
    if duplicate_count < 1:
        return -1
    # Assert that the point is inside the left & right edges
    for ax in range(dimension):
        root.cum_com[ax] *= frac_seen
        if (pos[ax] > (root.left_edge[ax] + root.width[ax] + EPSILON)):
            printf("[t-SNE] Error: point (%1.9e) is above right edge of node "
                    "(%1.9e)\n", pos[ax], root.left_edge[ax] + root.width[ax])
            return -1
        if (pos[ax] < root.left_edge[ax] - EPSILON):
            printf("[t-SNE] Error: point (%1.9e) is below left edge of node "
                   "(%1.9e)\n", pos[ax], root.left_edge[ax])
            return -1
    for ax in range(dimension):
        root.cum_com[ax] += pos[ax] * frac_new

    # If this node is unoccupied, fill it.
    # Otherwise, we need to insert recursively.
    # Two insertion scenarios: 
    # 1) Insert into this node if it is a leaf and empty
    # 2) Subdivide this node if it is currently occupied
    if (root.size == 0) & root.is_leaf:
        # Root node is empty and a leaf
        if DEBUGFLAG:
            printf("[t-SNE] [d=%i] Inserting [%f, %f] into blank cell\n", depth,
                   pos[0], pos[1])
        for ax in range(dimension):
            root.cur_pos[ax] = pos[ax]
        root.point_index = point_index
        root.size = duplicate_count
        return 0
    else:
        # Root node is occupied or not a leaf
        if DEBUGFLAG:
            printf("[t-SNE] [d=%i] Node %p is occupied or is a leaf.\n", depth,
                    root)
            printf("[t-SNE] [d=%i] Node %p leaf = %i. Size %i\n", depth, root,
                    root.is_leaf, root.size)
        if root.is_leaf & (root.size > 0):
            # is a leaf node and is occupied
            for ax in range(dimension):
                not_identical &= (fabsf(pos[ax] - root.cur_pos[ax]) < EPSILON)
                not_identical &= (root.point_index != point_index)
            if not_identical == 1:
                root.size += duplicate_count
                if DEBUGFLAG:
                    printf("[t-SNE] Warning: [d=%i] Detected identical "
                            "particles. Returning. Leaf now has size %i\n",
                            depth, root.size)
                return 0
        # If necessary, subdivide this node before
        # descending
        if root.is_leaf:
            if DEBUGFLAG:
                printf("[t-SNE] [d=%i] Subdividing this leaf node %p\n", depth,
                        root)
            subdivide(root)
        # We have two points to relocate: the one previously
        # at this node, and the new one we're attempting
        # to insert
        if root.size > 0:
            child = select_child(root, root.cur_pos, root.point_index)
            if DEBUGFLAG:
                printf("[t-SNE] [d=%i] Relocating old point to node %p\n",
                        depth, child)
            insert(child, root.cur_pos, root.point_index, depth + 1, root.size)
        # Insert the new point
        if DEBUGFLAG:
            printf("[t-SNE] [d=%i] Selecting node for new point\n", depth)
        child = select_child(root, pos, point_index)
        if root.size > 0:
            # Remove the point from this node
            for ax in range(dimension):
                root.cur_pos[ax] = -1            
            root.size = 0
            root.point_index = -1            
        return insert(child, pos, point_index, depth + 1, 1)

cdef int insert_many(QuadOctTree* tree, float[:,:] pos_array) nogil:
    # Insert each data point into the tree one at a time
    cdef long nrows = pos_array.shape[0]
    cdef long i
    cdef int ax
    cdef float row[3]
    cdef long err = 0
    for i in range(nrows):
        for ax in range(tree.dimension):
            row[ax] = pos_array[i, ax]
        if DEBUGFLAG:
            printf("[t-SNE] inserting point %i: [%f, %f]\n", i, row[0], row[1])
        err = insert(tree.root_node, row, i, 0, 1)
        if err != 0:
            printf("[t-SNE] ERROR\n")
            return err
        tree.n_particles += 1
    return err

cdef int free_tree(QuadOctTree* tree) nogil:
    cdef int check
    cdef long* cnt = <long*> malloc(sizeof(long) * 3)
    for i in range(3):
        cnt[i] = 0
    free_recursive(tree, tree.root_node, cnt)
    free(tree.root_node)
    free(tree)
    check = cnt[0] == tree.n_cells
    check &= cnt[2] == tree.n_particles
    free(cnt)
    return check

cdef void free_recursive(QuadOctTree* tree, QuadOctNode *root, long* counts) nogil:
    # Free up all of the tree nodes recursively
    # while counting the number of nodes visited
    # and total number of data points removed
    cdef int i, j, krange
    cdef int k = 0
    cdef QuadOctNode* child
    if root.tree.dimension > 2:
        krange = 2
    else:
        krange = 1
    if not root.is_leaf:
        for i in range(2):
            for j in range(2):
                for k in range(krange):
                    child = root.children[i][j][k]
                    free_recursive(tree, child, counts)
                    counts[0] += 1
                    if child.is_leaf:
                        counts[1] += 1
                        if child.size > 0:
                            counts[2] +=1
                    free(child)


cdef long count_points(QuadOctNode* root, long count) nogil:
    # Walk through the whole tree and count the number 
    # of points at the leaf nodes
    if DEBUGFLAG:
        printf("[t-SNE] Counting nodes at root node %p\n", root)
    cdef QuadOctNode* child
    cdef int i, j
    if root.tree.dimension > 2:
        krange = 2
    else:
        krange = 1
    for i in range(2):
        for j in range(2):
            for k in range(krange):
                # if this is a leaf node, there will be no children
                if root.is_leaf:
                    count += root.size
                    if DEBUGFLAG : 
                        printf("[t-SNE] %p is a leaf node, no children\n", root)
                        printf("[t-SNE] %i particles in node %p\n", count, root)
                    return count
                # otherwise, get the children
                else:
                    child = root.children[i][j][k]
                if DEBUGFLAG:
                    printf("[t-SNE] Counting points for child %p\n", child)
                if child.is_leaf and child.size > 0:
                    if DEBUGFLAG:
                        printf("[t-SNE] Child has size %d\n", child.size)
                    count += child.size
                elif not child.is_leaf:
                    if DEBUGFLAG:
                        printf("[t-SNE] Child is not a leaf. Descending\n")
                    count = count_points(child, count)
                # else case is we have an empty leaf node
                # which happens when we create a quadtree for
                # one point, and then the other neighboring cells
                # don't get filled in
    if DEBUGFLAG:
        printf("[t-SNE] %i particles in this node\n", count)
    return count


cdef void compute_gradient(float[:,:] val_P,
                           float[:,:] pos_reference,
                           long[:,:] neighbors,
                           float[:,:] tot_force,
                           QuadOctNode* root_node,
                           float theta,
                           long start,
                           long stop) nogil:
    # Having created the tree, calculate the gradient
    # in two components, the positive and negative forces
    cdef long i, coord
    cdef int ax
    cdef long n = pos_reference.shape[0]
    cdef int dimension = root_node.tree.dimension
    if root_node.tree.verbose > 11:
        printf("[t-SNE] Allocating %i elements in force arrays\n",
                n * dimension * 2)
    cdef float* sum_Q = <float*> malloc(sizeof(float))
    cdef float* neg_f = <float*> malloc(sizeof(float) * n * dimension)
    cdef float* pos_f = <float*> malloc(sizeof(float) * n * dimension)
    cdef clock_t t1, t2

    sum_Q[0] = 0.0
    if root_node.tree.verbose > 11:
        printf("[t-SNE] Computing positive gradient\n")
    t1 = clock()
    compute_gradient_positive_nn(val_P, pos_reference, neighbors, pos_f,
            dimension, start)
    t2 = clock()
    if root_node.tree.verbose > 15:
        printf("[t-SNE]  nn pos: %e ticks\n", ((float) (t2 - t1)))
    if root_node.tree.verbose > 11:
        printf("[t-SNE] Computing negative gradient\n")
    t1 = clock()
    compute_gradient_negative(val_P, pos_reference, neg_f, root_node, sum_Q, 
                              theta, start, stop)
    t2 = clock()
    if root_node.tree.verbose > 15:
        printf("[t-SNE] Negative: %e ticks\n", ((float) (t2 - t1)))
    for i in range(start, n):
        for ax in range(dimension):
            coord = i * dimension + ax
            tot_force[i, ax] = pos_f[coord] - (neg_f[coord] / sum_Q[0])
    free(sum_Q)
    free(neg_f)
    free(pos_f)

cdef void compute_gradient_positive(float[:,:] val_P,
                                    float[:,:] pos_reference,
                                    float* pos_f,
                                    int dimension) nogil:
    # Sum over the following expression for i not equal to j
    # grad_i = p_ij (1 + ||y_i - y_j||^2)^-1 (y_i - y_j)
    # This is equivalent to compute_edge_forces in the authors' code
    cdef:
        int ax
        long i, j, temp
        long n = val_P.shape[0]
        float buff[3]
        float D
    for i in range(n):
        for ax in range(dimension):
            pos_f[i * dimension + ax] = 0.0
        for j in range(n):
            if i == j : 
                continue
            D = 0.0
            for ax in range(dimension):
                buff[ax] = pos_reference[i, ax] - pos_reference[j, ax]
                D += buff[ax] ** 2.0  
            D = val_P[i, j] / (1.0 + D)
            for ax in range(dimension):
                pos_f[i * dimension + ax] += D * buff[ax]
                temp = i * dimension + ax


cdef void compute_gradient_positive_nn(float[:,:] val_P,
                                       float[:,:] pos_reference,
                                       long[:,:] neighbors,
                                       float* pos_f,
                                       int dimension,
                                       long start) nogil:
    # Sum over the following expression for i not equal to j
    # grad_i = p_ij (1 + ||y_i - y_j||^2)^-1 (y_i - y_j)
    # This is equivalent to compute_edge_forces in the authors' code
    # It just goes over the nearest neighbors instead of all the data points
    # (unlike the non-nearest neighbors version of `compute_gradient_positive')
    cdef:
        int ax
        long i, j, k
        long K = neighbors.shape[1]
        long n = val_P.shape[0]
        float[3] buff
        float D
    for i in range(start, n):
        for ax in range(dimension):
            pos_f[i * dimension + ax] = 0.0
        for k in range(K):
            j = neighbors[i, k]
            # we don't need to exclude the i==j case since we've 
            # already thrown it out from the list of neighbors
            D = 0.0
            for ax in range(dimension):
                buff[ax] = pos_reference[i, ax] - pos_reference[j, ax]
                D += buff[ax] ** 2.0  
            D = val_P[i, j] / (1.0 + D)
            for ax in range(dimension):
                pos_f[i * dimension + ax] += D * buff[ax]



cdef void compute_gradient_negative(float[:,:] val_P, 
                                    float[:,:] pos_reference,
                                    float* neg_f,
                                    QuadOctNode *root_node,
                                    float* sum_Q,
                                    float theta, 
                                    long start, 
                                    long stop) nogil:
    if stop == -1:
        stop = pos_reference.shape[0] 
    cdef:
        int ax
        long i
        long n = stop - start
        float* force
        float* iQ 
        float* pos
        int dimension = root_node.tree.dimension

    iQ = <float*> malloc(sizeof(float))
    force = <float*> malloc(sizeof(float) * dimension)
    pos = <float*> malloc(sizeof(float) * dimension)
    for i in range(start, stop):
        # Clear the arrays
        for ax in range(dimension): 
            force[ax] = 0.0
            pos[ax] = pos_reference[i, ax]
        iQ[0] = 0.0
        compute_non_edge_forces(root_node, theta, iQ, i,
                                pos, force)
        sum_Q[0] += iQ[0]
        # Save local force into global
        for ax in range(dimension): 
            neg_f[i * dimension + ax] = force[ax]
    free(iQ)
    free(force)
    free(pos)


cdef void compute_non_edge_forces(QuadOctNode* node, 
                                  float theta,
                                  float* sum_Q,
                                  long point_index,
                                  float* pos,
                                  float* force) nogil:
    # Compute the t-SNE force on the point in pos given by point_index
    cdef:
        QuadOctNode* child
        int i, j, krange
        int summary = 0
        int dimension = node.tree.dimension
        float dist2, mult, qijZ
        float wmax = 0.0
        float* delta  = <float*> malloc(sizeof(float) * dimension)
    
    if node.tree.dimension > 2:
        krange = 2
    else:
        krange = 1

    for i in range(dimension):
        delta[i] = 0.0

    # There are no points below this node if cum_size == 0
    # so do not bother to calculate any force contributions
    # Also do not compute self-interactions
    if node.cum_size > 0 and not (node.is_leaf and (node.point_index ==
        point_index)):
        dist2 = 0.0
        # Compute distance between node center of mass and the reference point
        for i in range(dimension):
            delta[i] += pos[i] - node.cum_com[i] 
            dist2 += delta[i]**2.0
        # Check whether we can use this node as a summary
        # It's a summary node if the angular size as measured from the point
        # is relatively small (w.r.t. to theta) or if it is a leaf node.
        # If it can be summarized, we use the cell center of mass 
        # Otherwise, we go a higher level of resolution and into the leaves.
        for i in range(dimension):
            wmax = max(wmax, node.width[i])

        summary = (wmax / sqrt(dist2) < theta)

        if node.is_leaf or summary:
            # Compute the t-SNE force between the reference point and the
            # current node
            qijZ = 1.0 / (1.0 + dist2)
            sum_Q[0] += node.cum_size * qijZ
            mult = node.cum_size * qijZ * qijZ
            for ax in range(dimension):
                force[ax] += mult * delta[ax]
        else:
            # Recursively apply Barnes-Hut to child nodes
            for i in range(dimension):
                for j in range(dimension):
                    for k in range(krange):
                        child = node.children[i][j][k]
                        if child.cum_size == 0: 
                            continue
                        compute_non_edge_forces(child, theta, sum_Q, 
                                                     point_index,
                                                     pos, force)

    free(delta)

def calculate_edge(pos_output):
    # Make the boundaries slightly outside of the data
    # to avoid floating point error near the edge
    left_edge = np.min(pos_output, axis=0)
    right_edge = np.max(pos_output, axis=0)
    center = (right_edge + left_edge) * 0.5
    width = np.maximum(np.subtract(right_edge, left_edge), EPSILON)
    # Exagerate width to avoid boundary edge
    width = width.astype(np.float32) * 1.001
    left_edge = center - width / 2.0
    right_edge = center + width / 2.0
    return left_edge, right_edge, width

def gradient(float[:,:] pij_input, 
             float[:,:] pos_output, 
             long[:,:] neighbors, 
             float[:,:] forces, 
             float theta,
             int dimension,
             int verbose,
             long skip_num_points=0):
    # This function is designed to be called from external Python
    # it passes the 'forces' array by reference and fills thats array
    # up in-place
    n = pos_output.shape[0]
    left_edge, right_edge, width = calculate_edge(pos_output)
    assert width.itemsize == 4
    assert pij_input.itemsize == 4
    assert pos_output.itemsize == 4
    assert forces.itemsize == 4
    m = "Number of neighbors must be < # of points - 1"
    assert n - 1 >= neighbors.shape[1], m
    m = "neighbors array and pos_output shapes are incompatible"
    assert n == neighbors.shape[0], m
    m = "Forces array and pos_output shapes are incompatible"
    assert n == forces.shape[0], m
    m = "Pij and pos_output shapes are incompatible"
    assert n == pij_input.shape[0], m
    m = "Pij and pos_output shapes are incompatible"
    assert n == pij_input.shape[1], m
    m = "Only 2D and 3D embeddings supported. Width array must be size 2 or 3"
    assert width.shape[0] <= 3, m
    if verbose > 10:
        printf("[t-SNE] Initializing tree of dimension %i\n", dimension)
    cdef QuadOctTree* qt = init_tree(left_edge, width, dimension, verbose)
    if verbose > 10:
        printf("[t-SNE] Inserting %i points\n", pos_output.shape[0])
    err = insert_many(qt, pos_output)
    assert err == 0, "[t-SNE] Insertion failed"
    if verbose > 10:
        printf("[t-SNE] Computing gradient\n")
    compute_gradient(pij_input, pos_output, neighbors, forces, qt.root_node, 
                     theta, skip_num_points, -1)
    if verbose > 10:
        printf("[t-SNE] Checking tree consistency \n")
    cdef long count = count_points(qt.root_node, 0)
    m = ("Tree consistency failed: unexpected number of points=%i "
         "at root node=%i" % (count, qt.root_node.cum_size))
    assert count == qt.root_node.cum_size, m 
    m = "Tree consistency failed: unexpected number of points on the tree"
    assert count == qt.n_particles, m
    free_tree(qt)

# Helper functions
def check_quadtree(X, long[:] counts):
    """
    Helper function to access quadtree functions for testing
    """
    
    X = X.astype(np.float32)
    left_edge, right_edge, width = calculate_edge(X)
    # Initialise a tree
    qt = init_tree(left_edge, width, 2, 2)
    # Insert data into the tree
    insert_many(qt, X)

    cdef long count = count_points(qt.root_node, 0)
    counts[0] = count
    counts[1] = qt.root_node.cum_size
    counts[2] = qt.n_particles
    free_tree(qt)
    return counts

cdef void get_quadtree_params(QuadOctNode *root,
                              np.ndarray[np.float64_t, ndim=1] width_maxima,
                              np.ndarray[np.float64_t, ndim=1] width_minima,
                              np.ndarray[np.int64_t, ndim=1] level_max):
    """
    Traverse the tree, keeping track of the parameters as we descend.
    """
    cdef int i, j, krange
    cdef int k = 0
    cdef QuadOctNode* child
    if root.tree.dimension > 2:
        krange = 2
    else:
        krange = 1

    # update the maximum depth of the tree (the level)
    if root.level > level_max[0]:
        level_max[0] = root.level

    # update the width maxima and minima
    for ix in range(root.tree.dimension):
        if root.width[ix] > width_maxima[ix]:
            width_maxima[ix] = root.width[ix]
        if root.width[ix] < width_minima[ix]:
            width_minima[ix] = root.width[ix]

    # recursively descend if this is not a leaf node
    for i in range(2):
        for j in range(2):
            for k in range(krange):
                if not root.is_leaf:
                    child = root.children[i][j][k]
                    get_quadtree_params(child, width_maxima, width_minima, level_max)

def check_quadtree_api(X):
    """
    Helper function to test the functionality of quadtree API.
    Returns a dictionary object of the quadtree.
    """

    X = X.astype(np.float32)
    left_edge, right_edge, width = calculate_edge(X)
    # Initialise a tree
    qt = init_tree(left_edge, width, X.shape[1], 2)
    # Insert data into the tree
    insert_many(qt, X)

    properties = {}
    properties['n_cells'] = qt.n_cells
    properties['n_particles'] = qt.n_particles
    properties['dimension'] = qt.dimension
    width_maxima = np.zeros(qt.dimension, dtype=np.float64)
    width_minima = np.array(width, dtype=np.float64)
    level_max = np.zeros(1, dtype=np.int64)
    get_quadtree_params(qt.root_node, width_maxima, width_minima, level_max)
    properties['width_max'] = width_maxima
    properties['width_min'] = width_minima
    properties['level_max'] = level_max

    free_tree(qt)

    return properties
